// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext {
        kotlin_version = '1.7.20'
        hilt_version = '2.44'
        exposed_version = '0.40.1'
        krate_version = '2.0.0'
        splitties_version = '3.0.0'
    }
}

plugins {
    id 'com.android.application' version '7.2.1' apply false
    id 'com.android.library' version '7.2.1' apply false
    id 'org.jetbrains.kotlin.android' version "$kotlin_version" apply false
    id 'org.jetbrains.kotlin.plugin.serialization' version "$kotlin_version" apply false
    id 'com.google.dagger.hilt.android' version "$hilt_version" apply false
    id 'org.ec4j.editorconfig' version "0.0.3" apply false
    id "org.jetbrains.gradle.plugin.idea-ext" version "1.1.5" apply true
}

// External project configuration start

// Configure and automatically generate AAR of the below mentioned external
// projects using a custom task  in order to let Android Studio work properly.
// MUST BE RUN MANUALLY ON CLI COMPILATION
def external_projects = [
        project(':external:calendar'),
        project(':external:chips'),
        project(':external:colorpicker'),
        project(':external:timezonepicker')
]

configure(external_projects) {
    apply {
        from '../configuration/common.gradle'
    }
}

task aarGen {
    description "Generates AAR from the external projects for Etar-Calendar"
    def aarTasks = [
            ':external:calendar:copyAAR',
            ':external:chips:copyAAR',
            ':external:colorpicker:copyAAR',
            ':external:timezonepicker:copyAAR'
    ]
    dependsOn aarTasks
}

idea.project.settings {
    taskTriggers {
        afterSync tasks.getByName('aarGen')
    }
}
// External project configuration end

task clean(type: Delete) {
    delete rootProject.buildDir
}
